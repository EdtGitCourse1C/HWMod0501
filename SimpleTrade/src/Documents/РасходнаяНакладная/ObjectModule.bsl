Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	                      |	ВЫБОР
	                      |		КОГДА РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК ЭтоУслуга,
	                      |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	                      |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	                      |ПОМЕСТИТЬ ДокТЧ
	                      |ИЗ
	                      |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	                      |ГДЕ
	                      |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &ТекущийДокумент
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	                      |	ВЫБОР
	                      |		КОГДА РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	Номенклатура
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДокТЧ.Номенклатура КАК Номенклатура,
	                      |	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	                      |	ДокТЧ.Количество КАК Количество,
	                      |	ДокТЧ.Сумма КАК Сумма,
	                      |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	                      |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
	                      |ИЗ
	                      |	ДокТЧ КАК ДокТЧ
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	                      |				&Период,
	                      |				Номенклатура В
	                      |					(ВЫБРАТЬ
	                      |						ДокТЧ.Номенклатура
	                      |					ИЗ
	                      |						ДокТЧ КАК ДокТЧ
	                      |					ГДЕ
	                      |						НЕ ДокТЧ.ЭтоУслуга)) КАК ОстаткиНоменклатурыОстатки
	                      |		ПО ДокТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	                      |			И (НЕ ДокТЧ.ЭтоУслуга)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Партия" + ?(РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(МоментВремени()).Значение = Перечисления.УчетнаяПолитика.ЛИФО, " УБЫВ", "") + "
	                      |ИТОГИ
	                      |	МАКСИМУМ(Количество),
	                      |	МАКСИМУМ(Сумма),
	                      |	СУММА(КоличествоОстаток),
	                      |	СУММА(СтоимостьОстаток)
	                      |ПО
	                      |	Номенклатура");
	//
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	//Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.Продажи.Записывать = Истина;
	//
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
		ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	//
	Запрос.УстановитьПараметр("Период", ?(Режим = РежимПроведенияДокумента.Неоперативный, МоментВремени(), Неопределено));
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	ВыборкаПоНоменклатуре = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//
	Пока ВыборкаПоНоменклатуре.Следующий() Цикл
		Стоимость = 0;
		Если НЕ ВыборкаПоНоменклатуре.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			Если ВыборкаПоНоменклатуре.Количество > ВыборкаПоНоменклатуре.КоличествоОстаток Тогда
				Сообщить("Не хватает "
						+ СокрЛП(ВыборкаПоНоменклатуре.Количество - ВыборкаПоНоменклатуре.КоличествоОстаток)
						+ " ед. товара " + СокрЛП(ВыборкаПоНоменклатуре.Номенклатура)
						+ ", на остатках есть только "
						+ СокрЛП(ВыборкаПоНоменклатуре.КоличествоОстаток)
						+ " из " + СокрЛП(ВыборкаПоНоменклатуре.Количество)
						+ " затребованных.");
				Отказ = Истина;
				Продолжить;
			КонецЕсли;
			ВыборкаПоПартиям = ВыборкаПоНоменклатуре.Выбрать();
			ОсталосьСписать = ВыборкаПоНоменклатуре.Количество;
			Пока ВыборкаПоПартиям.Следующий() И ОсталосьСписать > 0 Цикл
				Движение = Движения.ОстаткиНоменклатуры.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаПоПартиям.Номенклатура;
				Движение.Партия = ВыборкаПоПартиям.Партия;
				Движение.Количество = Мин(ВыборкаПоПартиям.КоличествоОстаток, ОсталосьСписать);
				Движение.Стоимость = ?(ВыборкаПоПартиям.КоличествоОстаток > ОсталосьСписать, ОсталосьСписать * ВыборкаПоПартиям.СтоимостьОстаток / ВыборкаПоПартиям.КоличествоОстаток,ВыборкаПоПартиям.СтоимостьОстаток);
				//
				ОсталосьСписать = ОсталосьСписать - Движение.Количество;
				Стоимость = Стоимость + Движение.Стоимость;
			КонецЦикла;
		КонецЕсли;
		//
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаПоНоменклатуре.Номенклатура;
		Движение.Количество = ВыборкаПоНоменклатуре.Количество;
		Движение.Сумма = ВыборкаПоНоменклатуре.Сумма;
		Движение.Стоимость = Стоимость;
	КонецЦикла;
КонецПроцедуры